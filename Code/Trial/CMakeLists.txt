SET(BTKTrial_SRCS
  btkEvent.cpp
  btkTimeSequence.cpp
  btkTrial.cpp
)

ADD_LIBRARY(Trial ${BTK_LIBS_BUILD_TYPE} ${BTKTrial_SRCS})
TARGET_INCLUDE_DIRECTORIES(Trial PUBLIC
  $<BUILD_INTERFACE:${BTK_BINARY_DIR}>
  $<BUILD_INTERFACE:${BTK_BINARY_DIR}/Code/Base>
  $<BUILD_INTERFACE:${BTK_SOURCE_DIR}/Code/Base>
  $<BUILD_INTERFACE:${BTK_SOURCE_DIR}/ThirdParties/eigen3>
  $<INSTALL_INTERFACE:include/btk>
  $<INSTALL_INTERFACE:include/btk/3rdparty/eigen3>
)
GENERATE_EXPORT_HEADER(Trial
                       BASE_NAME BTK_TRIAL
                       EXPORT_FILE_NAME btkTrialExport.h)

SET_TARGET_PROPERTIES(Trial PROPERTIES ${BTK_LIBRARY_PROPERTIES})
IF (NOT ${BTK_BUILD_SHARED_LIBS})
  SET_TARGET_PROPERTIES(Trial PROPERTIES COMPILE_FLAGS -DBTK_TRIAL_STATIC_DEFINE)
ENDIF()

INSTALL(TARGETS Trial EXPORT BTKTargets
  LIBRARY DESTINATION lib
  ARCHIVE DESTINATION lib
  RUNTIME DESTINATION bin
  INCLUDES DESTINATION include
)

FILE(GLOB __files "${CMAKE_CURRENT_SOURCE_DIR}/*.h")
FILE(GLOB __private "${CMAKE_CURRENT_SOURCE_DIR}/*_p.h")
IF (__private) 
  LIST(REMOVE_ITEM __files ${__private})
ENDIF ()
INSTALL(
  FILES 
    ${__files}
    ${CMAKE_CURRENT_BINARY_DIR}/btkTrialExport.h
  DESTINATION include
  COMPONENT dev
)
